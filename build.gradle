buildscript {
    ext.kotlin_version = '1.3.70'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
    }
}

final v = getPublishVersion()

allprojects {
    group = "com.github.ajalt"
    version = v
    repositories {
        mavenCentral()
        jcenter()
    }
}

@SuppressWarnings("GrMethodMayBeStatic") // false-positive; we access VERSION_NAME
private String getPublishVersion() {
    // Call gradle with -PinferVersion to set the dynamic version name. Otherwise we skip it to save time.
    if(!hasProperty("inferVersion")) return VERSION_NAME

    final tag = "git tag --points-at master".execute().text.trim()
    if (!tag.isEmpty()) return tag

    final buildNumber = "git rev-list --count master".execute().text.trim()
    if (!buildNumber.isEmpty()) {
        return VERSION_NAME + "." + buildNumber + "-SNAPSHOT"
    }
    return VERSION_NAME
}
